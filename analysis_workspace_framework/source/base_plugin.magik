#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:base_plugin)
$

_pragma(classify_level=advanced, topic={analysis})
## Base plugin for all plugins in the analysis workspace frameworkd.
def_slotted_exemplar(:base_plugin,
	{
		{:workspace, _unset},
		{:selection, _unset}
	}, :plugin)
$

base_plugin.def_property(:runner_plugin_name, :type, :symbol)
$

_pragma(classify_level=restricted)
base_plugin.define_shared_constant( :databus_consumer_data_types,
	{ :workspace, :selection, :map_selection, :record_selection, :map_trail},
	:public )
$

_pragma(classify_level=restricted)
base_plugin.define_shared_constant( :databus_producer_data_types,
	{ :workspace, :selection },
	:public )
$

_pragma(classify_level=restricted, topic={analysis}, usage={subclass})
base_plugin.define_shared_constant( :dialog_exemplar_name,
	_unset,
	:public )
$

_pragma(classify_level=restricted, topic={analysis})
_private _method base_plugin.init(_gather a)
	 _super.init(_scatter a)
	.selection << rope.new()
	 _local dialog << _self.dialog_exemplar.new(_self)
	_self.cache_dialog(:dialog, dialog)
	 _return _self
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method base_plugin.dialog_exemplar
	_return package(:rwan)[_self.dialog_exemplar_name]
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method base_plugin.dialog
	_return _self.get_dialog(:dialog)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method base_plugin.selection
	_return .selection
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.workspace
	_return .workspace 
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.selected_holders
	_return .selection
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.selected_top_holders
	_return _self.workspace.top_holders(.selection)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.all_holders
	_local cur << _self.workspace
	_if cur _isnt _unset
	_then 
		_return cur.all_holders
	_else
		_return {}
	_endif 
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.top_holders
	_local cur << _self.workspace
	_if cur _isnt _unset
	_then 
		_return cur.top_holders
	_else
		_return {}
	_endif 
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.app_plugin
	_return .framework.app_plugin
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.ace
	_return _self.app_plugin.ace
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.open?
	>> .workspace.open?
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.build_gui(container)
	_self.dialog.build_embedded_gui(container)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.post_activation()
	## It is guaranteed that all GUI components will be realised.
	_self.dialog.assign_top_frame(_self.framework.top_frame)
	_self.manage_actions()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.publish_workspace(_optional holder)
	_self.databus_make_data_available(:workspace, holder.default(.workspace))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.publish_selection(_optional selection)
	_self.databus_make_data_available(:selection, selection.default(.selection))
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.manage_actions()
	
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method base_plugin.sw_databus_data_available(data_type_name, data, sender)
	_if data_type_name _is :selection
	_then
		_self.note_selection_change(data)
	_elif data_type_name _is :workspace
	_then
		_self.note_workspace_change(data)
	_elif data_type_name _is :map_selection
	_then
		_self.note_map_selection_change(data)
	_elif data_type_name _is :record_selection
	_then
		_self.note_map_record_selection_change(data)
	_elif data_type_name _is :map_trail
	_then
		_self.note_map_trail_change(data)
	_endif
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method base_plugin.note_selection_change(selection)
	.selection << selection
	_self.dialog.note_selection_change()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method base_plugin.note_map_selection_change(data)
	_self.dialog.note_map_selection_change(data)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method base_plugin.note_map_record_selection_change(data)
	_self.dialog.note_map_record_selection_change(data)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method base_plugin.note_map_trail_change(data)
	_self.dialog.note_map_trail_change(data)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method base_plugin.note_workspace_change(workspace)
	_local renew? << .workspace _isnt workspace
	.workspace << workspace

	_local dialog << _self.dialog
	_if dialog _isnt _unset
	_then 
		dialog.note_workspace_change(renew?)
	_endif 
_endmethod
$
