#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#


_package rwan
$

#remex(:runner_dialog)
$

_pragma(classify_level=advanced, topic={analysis})
## Provide a GUI for executing (in background) and reporting
## the results of an analysis collection.
def_slotted_exemplar(:runner_dialog,
	{
		{:runner, _unset}
	},
	:base_model)
$

message_handler.new(:runner_dialog).add_uses(:base_model)
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.activate_in(container)
	.top_frame << container.top_frame
	
	_local rc << rowcol.new(container,  1, _unset, _unset)
	.items[:busy_with] << sw_label_item.new(rc, _unset ,
					     :max_width, 100,
					     :min_width, 100)
	
	.items[:interrupt] << image_button_item.new_safe(rc, _self.stop_image,
							  _self, :|cancel_run()|,
							  :insensitive_image, _self.grey_stop_image, 
							  :col_alignment, :right)
	.items[:interrupt].set_property(:has_border?, _false)
	
	.runner << background_runner.new(.items[:busy_with])
	.runner.add_dependent(_self)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.refresh_map()
	.owner.refresh_map()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.cancel_run()
	.runner.cancel_run()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.run_in_background(receiver, meth, args, descr)
	.runner.run_in_background(receiver, meth, args, descr)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.running?
	_return .runner.running?	
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.report_sums()
	_local holder << _self.selected_holder
	_local dia << report_grouping_and_calculation_dialog.new_dialog(_self)
	dia.set_holder(holder)
	dia.activate()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.goto()
	_local descr <<  _self.selection_description()
	_self.run_in_background(_self, :|do_goto()|, {}, _self.message(:going_to, _unset, descr))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.do_goto()
	_local b
	_for i_holder _over _self.selected_holders.fast_elements()
	_loop
		_local col << i_holder.collection
		_local bounds << col.goto_bounds
		_if bounds _isnt _unset
		_then 
			b << bounds.union(b)
		_endif 
	_endloop
	_if b _isnt _unset
	_then 
		.owner.goto(b)
	_else
		_self.user_error(:no_bounds_to_goto)
	_endif 
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.goto_one()
	_local descr <<  _self.selection_description()
	_self.run_in_background(_self, :|do_goto_one()|, {}, _self.message(:going_to, _unset, descr))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.do_goto_one()
	_local bounds
	_local holder << _self.selected_holder
	_local col << holder.collection
	_local element << col.an_element()
	_if element _isnt _unset
	_then
		_try 
			bounds << element.goto_bounds
		_when error
			bounds << element.bounds
		_endtry 
	_endif 
	_if bounds _isnt _unset
	_then 
		.owner.goto(bounds)
	_else
		_self.user_error(:no_bounds_to_goto)
	_endif 
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.send_to_explorer()
	_local holder << _self.selected_holder
	_if holder.is_record_collection?.not _then _return _endif
	
	_local descr << _self.selection_description()
	_self.run_in_background(_self, :|do_send_to_explorer()|, {}, _self.message(:sending_to_explorer, _unset, descr))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.do_send_to_explorer()
	_local col << _self.selected_holder.collection
	_local lazy << lazy_record_collection.new_for_rw_analysis_collection(col)
	.owner.explore(lazy)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.export_to_excel()
	_self.run_in_background(_self, :|do_export_to_excel()|, {},
				_self.message(:export_to_excel))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.do_export_to_excel()
	_local name << _self.selected_top_holder.name
	_local col << _self.selected_top_holder.collection
	_local lazy << lazy_record_collection.new_for_rw_analysis_collection(col)
	
	_local fname << system.temp_file_name("export.xlsx")
	_local engine << collection_export_engine.new( _self )
	engine.add_collection(lazy,
			      :exclude_geometry_fields?, _true,
			      :unit_fields_as_strings?, _true,
			      :external_name, name)
	engine.export_to_excel( :run_in_background?, _false,
				:filename, fname,
				:quiet?, _true,
				:overwrite?, _true)
	engine.reset()
	system.start_command({"explorer", fname})
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.export_to_access()
	_self.run_in_background(_self, :|do_export_to_access()|, {},
				_self.message(:export_to_access))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.do_export_to_access()
	_local name << _self.selected_top_holder.name
	_local col << _self.selected_top_holder.collection
	_local lazy << lazy_record_collection.new_for_rw_analysis_collection(col)
	
	_local fname << system.temp_file_name("export.accdb")
	_local engine << collection_export_engine.new( _self )
	engine.add_collection(lazy,
			      :exclude_geometry_fields?, _false,
			      :exclude_join_fields?, _false,
			      :max_length_multiple_result_joins, 0,
			      :unit_fields_as_strings?, _true,
			      :join_fields_as_count?, _true,
			      :ace, .owner.ace,
			      :external_name, name)
	engine.export_to_access( :run_in_background?, _false,
				:filename, fname)
	engine.reset()
	system.start_command({"explorer", fname})
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.note_selection_change()
	_self.manage_actions()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method runner_dialog.selection_description()
	_local holders << _self.selected_holders
	_if holders.size > 1
	_then
		descr << _self.message(:multiple_selection, _unset, holders.size)
	_else 
		descr <<  holders[1][:name]
	_endif
	_return descr
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.manage_actions()
	_local sel << _self.selected_holder
	_local select? << sel _isnt _unset
	_local record_sel? << select? _andif
			      sel.is_record_collection?
	
	_local open? << _self.open?
	
	.owner.action(:explorer).enabled? << open? _andif record_sel?
	.owner.action(:goto).enabled? << open? _andif select?
	.owner.action(:goto_one).enabled? << open? _andif select?
	.owner.action(:refresh_map).enabled? << _true 
	.owner.action(:excel).enabled? << open? _andif record_sel?
	.owner.action(:access).enabled? << open? _andif record_sel?
	.owner.action(:calc).enabled? << open? _andif record_sel?
	
	_local running? << _self.running?
	.items[:interrupt].enabled? << running?
	.items[:busy_with].enabled? << running?
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method runner_dialog.note_change(who, what)
	_self.manage_actions()
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_private _method runner_dialog.stop_image
	_return smallworld_product.get_raster_image(:stop, :ui_resources)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method runner_dialog.grey_stop_image
	_return smallworld_product.get_raster_image(:stop_grey, :ui_resources)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method runner_dialog.export_excel_image
	_return smallworld_product.get_raster_image(:export_to_excel, :ui_resources)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method runner_dialog.grey_export_excel_image
	_return smallworld_product.get_raster_image(:export_to_excel_grey, :ui_resources)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method runner_dialog.export_access_image
	_return smallworld_product.get_raster_image(:export_to_access, :ui_resources)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method runner_dialog.grey_export_access_image
	_return smallworld_product.get_raster_image(:export_to_access_grey, :ui_resources)
_endmethod
$



