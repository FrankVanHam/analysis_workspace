#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:construct_dialog)
$

_pragma(classify_level=advanced, topic={analysis})
## Dialog that will add and create analysis collections.
def_slotted_exemplar(:construct_dialog,
	{},
	:base_model)
$

message_handler.new(:construct_dialog).add_uses(:base_model)
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.owner
	_return .owner	
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.run_in_background(meth, descr)
	.owner.run_in_background(_self, meth, {}, descr)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.update_holder(old_holder, new_holder)
	_self.workspace.set_dirty()
	workspace.update(old_holder, new_holder)
	.owner.publish_workspace()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_holder(holder)
	_self.add_holders({holder})
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_holders(holders)
	_local workspace << _self.workspace
	workspace.add_holders(holders)
	.owner.publish_workspace()
	.owner.publish_selection({holders.last})
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.new_holder(col, _optional a_msg)
	_local clr << colour.called(:red)
	_local msg << _if a_msg _isnt _unset
		      _then >> a_msg
		      _else >> _self.name_for_collection(col)
		      _endif 
	>>  collection_holder.new(col,
				  msg,
				  clr)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.name_for_collection(col)
	_return col.description(_self.workspace)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit()
	_return _self.selected_holder.activate_editor_in(_self)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.view()
	_local editor << _self.edit()
	editor.set_readonly()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.delete()
	## delete the collections, but only the top level collections
	## to avoid creating inconsistencies.
	_local workspace << _self.workspace
	_local to_del << _self.selected_top_holders.copy()	
	_if to_del.empty? _then _return _endif

	_local ok? << _self.can_delete?(to_del)
	_if _not ok? _then _return _endif 
	
	_for i_holder _over to_del.fast_elements()
	_loop
		i_holder.discard()
		workspace.remove_holder(i_holder)
	_endloop
	.owner.publish_workspace()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.can_delete?(to_del)
	_return _self.question_alert( :yes, :no,
				      :sure_to_delete, to_del.size)
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_collections_from_dialog()
	_local dia << object_control_dialog.new_dialog(_self)
	dia.set_dataset(.owner.dataset, _self.dataset_manager)
	dia.activate_relative_to(_unset, .top_frame, 50, 50 )
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.copy()
	_local cp << _self.selected_holder.copy()
	cp.name << _self.message(:copy_holder, _unset, cp.name)
	cp.collection.dp
	_self.add_holder(cp)
	.owner.publish_workspace()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.move_up()
	_local workspace << _self.workspace
	_local holders << _self.selected_top_holders
	_for i_holder _over holders.fast_elements()
	_loop
		workspace.move_up(i_holder)
	_endloop
	.owner.publish_workspace()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.can_move_up?
	_local workspace << _self.workspace
	_local holders << _self.selected_top_holders
	_for i_holder _over holders.fast_elements()
	_loop
		_if workspace.can_move_up?(i_holder)
		_then
			_return _true
		_endif
	_endloop
	_return _false
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.move_down()
	_local workspace << _self.workspace
	_local holders << _self.selected_top_holders
	_for i_holder _over holders.fast_elements()
	_loop
		workspace.move_down(i_holder)
	_endloop
	.owner.publish_workspace()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.can_move_down?
	_local workspace << _self.workspace
	_local holders << _self.selected_top_holders
	_for i_holder _over holders.fast_elements()
	_loop
		_if workspace.can_move_down?(i_holder)
		_then
			_return _true
		_endif
	_endloop
	_return _false
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_trail()
	_local dsm << _self.dataset_manager
	_local pseudo << _self.trail
	col << rwan:geometry_collection.new(dsm, geometry_set.new_with(pseudo))
	_if pseudo.geom_category _is :area
	_then
		msg << _self.message(:closed_trail_col_name)
	_else
		msg << _self.message(:trail_col_name)
	_endif
	_self.add_holder(_self.new_holder(col, msg))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_geom_selection()
	_local sel << .owner.map_selection
	_local sel_by_type << hash_table.new()
	_for i_geom _over sel.fast_elements()
	_loop
		_local rwo << i_geom.rwo
		_if rwo _isnt _unset
		_then 
			sel_by_type.at_or_new_if_absent(rwo.source.name, rwo_set).add(rwo)
		_endif 
	_endloop
	_local holders << rope.new()
	_local dsm << _self.dataset_manager
	_for type, set _over sel_by_type.fast_keys_and_elements()
	_loop
		col << rwan:record_set_collection.new(dsm, set)
		msg << _self.message(:record_selection, _unset, set.size, set.an_element().external_name)
		holders.add(_self.new_holder(col, msg))
	_endloop
	_self.add_holders(holders)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.union_areas(areas)
	_local un
	_local world
	_for i_area _over areas.fast_elements()
	_loop
		_if un _is _unset
		_then
			un << pseudo_area.new_for_world(i_area, i_area.world)
		_else
			w << un.world
			un << _self.safe_union(un, i_area)
			un.world << w
		_endif
	_endloop
	_return un
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.safe_union(un, an_area)
	_try
		_handling error _with _proc(c)
					      c.report_contents_on(!output!)
					      !traceback!(!output!)
		_endproc

		new_un << un.union(an_area)

	_when polygon_acp_invalid_area
		new_un << un
	_endtry
	_return new_un
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_collection_selection()
	_self.run_in_background(:|do_add_collection_selection()|, _self.message(:adding_selection_from_explorer))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.do_add_collection_selection()
	_local dsm << _self.dataset_manager
	_local sel << .owner.map_selection
	_local cols << set.new()
	_for i_geom _over sel.fast_elements()
	_loop
		_local rwo << i_geom.rwo
		_if rwo _is _unset _then _continue _endif
		cols.add(rwo.source_collection)
	_endloop
	_local holders << rope.new()
	_for i_col _over cols.fast_elements()
	_loop
		_local col << rwan:sw_collection.new(dsm, i_col)
		holders.add(_self.new_holder(col, i_col.external_name.copy()))
	_endloop
	_self.add_holders(holders)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_explorer_content()
	_self.run_in_background(:|do_add_explorer_content()|, _self.message(:adding_from_explorer))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.do_add_explorer_content()
	# Add the explorer content, derived from the explorer
	# selection.
	#
	_local dsm << _self.dataset_manager
	_local data << .owner.explorer_selection
	_if data _is _unset
	_then
		_self.user_error(:sorry_no_explorer)
	_endif
	_local (plug, sel) << (_scatter data)
	_local holders << rope.new()
	_for i_elem _over  sel.explorer_elements()
	_loop
		_for i_set _over i_elem.rwo_sets()
		_loop
			_if i_set.is_kind_of?(ds_collection)
			_then
				col << rwan:sw_collection.new(dsm, i_set)
			_else
				col << rwan:record_set.new(dsm, i_set)
			_endif
			holders.add(_self.new_holder(col))
		_endloop 
	_endloop
	_self.add_holders(holders)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_explorer_selection()
	_local dsm << _self.dataset_manager
	_local data << .owner.explorer_selection
	_if data _is _unset
	_then
		_self.user_error(:sorry_no_explorer)
	_endif
	_local (plug, sel) << (_scatter data)
	_local holders << rope.new()
	_if sel.is_records?
	_then
		set << rwo_set.new_from_iter(sel, :|records()|)
		col << rwan:record_set.new(dsm, set)
		holders.add(_self.new_holder(col))
	_else
		_self.user_error(:please_select_records_in_explorer)
	_endif
	_self.add_holders(holders)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.add_scrapbook()
	_local sel << .owner.scrapbook_results
	_if sel _is _unset
	_then
		_self.user_error(:scrapbook_is_empty)
	_else
		_self.run_in_background(:|do_add_scrapbook()|, _self.message(:adding_from_scrapbook))
	_endif
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.do_add_scrapbook()
	_local dsm << _self.dataset_manager
	_local sel << .owner.scrapbook_results
	_local sel_by_table_name << hash_table.new()

	_local holders << rope.new()
	_for i_rec_set _over sel.fast_elements()
	_loop
		_for i_rec _over i_rec_set.fast_elements()
		_loop
			sel_by_table_name.at_or_new_if_absent(
				i_rec.source_collection.name,
				rope).add(i_rec)
		_endloop
		_for set _over sel_by_table_name.fast_elements()
		_loop
			_local col << rwan:record_set.new(dsm, set)
			holders.add(_self.new_holder(col))
		_endloop
	_endloop
	_self.add_holders(holders)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.trail
	_local geom << .owner.map_trail.as_pseudo_geometry
	_if geom _is _unset
	_then
		_self.user_error(:provide_trail)
	_endif
	geom.style_info << _unset
	_return geom
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_intersection()
	_local holders << _self.selected_top_holders
	_local holder1 << holders[1]
	_local holder2 << holders[2]
	_local new_col << holder1.collection.intersection(holder2.collection)
	_self.add_holder(_self.new_holder(new_col))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_union()
	_local holders << _self.selected_top_holders
	_local holder1 << holders[1]
	_local holder2 << holders[2]
	_self.ensure_set_operation_makes_sense(holder1, holder2)
	_local new_col << holder1.collection.union(holder2.collection)
	_self.add_holder(_self.new_holder(new_col))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_xor()
	_local holders << _self.selected_top_holders
	_local holder1 << holders[1]
	_local holder2 << holders[2]
	_self.ensure_set_operation_makes_sense(holder1, holder2)
	_local new_col << holder1.collection.xor(holder2.collection)
	_self.add_holder(_self.new_holder(new_col))
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_subtraction()
	_local holders << _self.selected_top_holders
	_local holder1 << holders[1]
	_local holder2 << holders[2]
	_self.ensure_set_operation_makes_sense(holder1, holder2)
	_local dia << record_subtraction_dialog.new_dialog(_self)
	dia.set_holders(holder1, holder2, _self.dataset_manager)
	dia.activate()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_spatial_subtraction()
	_local (holder1,holder2) << _self.selected_spatial_and_spatial()
	_self.ensure_set_operation_makes_sense(holder1, holder2)
	_local dia << spatial_subtraction_dialog.new_dialog(_self)
	dia.set_holder(holder1, holder2, _self.dataset_manager)
	dia.activate()
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.ensure_set_operation_makes_sense(holder1, holder2)
	_if holder1.collection.is_record_collection? _andif
	    holder2.collection.is_record_collection?
	_then
		_if holder1.collection.expose() _isnt holder2.collection.expose()
		_then
			_self.user_error(:set_operator_must_have_same_base)
		_endif
	_endif
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.materialize_spatial()
	_local holder << _self.selected_top_holder
	_local dia << spatial_materialize_dialog.new_dialog(_self)
	dia.set_holder(holder, _self.dataset_manager)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.materialize()
	_local holder << _self.selected_top_holder
	_local dia << record_materialize_dialog.new_dialog(_self)
	dia.set_holder(holder, _self.dataset_manager)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_join_view()
	_local holder << _self.selected_top_holder
	_self.ensure_collection_is_sw_collection(holder)
	_local dia << sw_view_join_dialog.new_dialog(_self)
	dia.set_holder(holder, _self.dataset_manager)
	dia.activate()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_eq_view()
	_local holders << _self.selected_top_holders
	_self.ensure_collection_is_sw_collection(holders[1])
	_self.ensure_collection_is_sw_collection(holders[2])
	_local dia << sw_view_eq_dialog.new_dialog(_self)
	dia.set_holder(holders[1], holders[2], _self.dataset_manager)
	dia.activate()
	_return dia
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.ensure_collection_is_sw_collection(holder)
	_if _not holder.collection.is_sw_collection?
	_then
		_self.user_error(:view_only_possible_on_sw_collection)
	_endif
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_spatial_collection()
	_local holder << _self.selected_top_holder
	_local dia << spatial_dialog.new_dialog(_self)
	dia.set_holder(holder, _self.dataset_manager)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_buffer()
	_local holder << _self.selected_top_holder
	_local dia << buffer_dialog.new_dialog(_self)
	dia.set_holder(holder, _self.dataset_manager)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_follow_topology_dialog()
	_local dia << topology_dialog.new_dialog(_self)
	dia.set_source_holder(_self.workspace, _self.selected_holder)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit_follow_topology_dialog(holder, source_col, source_geom_name, target_col)
	_local source_holder << _self.workspace.holder_for(source_col)
	
	_local dia << topology_dialog.new_dialog(_self)
	dia.set_holder(_self.workspace,
		       holder, source_holder, source_geom_name,
		       target_col)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_follow_trace_dialog()
	_local dia << trace_dialog.new_dialog(_self)
	dia.set_source_holder(_self.workspace, _self.selected_holder)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit_follow_trace_dialog(holder, source_col, source_geom_name, stop_predicates, max_length, target_col)
	_local source_holder << _self.workspace.holder_for(source_col)
	_local dia << trace_dialog.new_dialog(_self)
	dia.set_holder(_self.workspace,
		       holder, source_holder, source_geom_name,
		       stop_predicates, max_length, target_col)
	dia.activate()
	_return dia
_endmethod
$
_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_follow_root_dialog()
	
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit_follow_root_dialog(holder, base_col, target_col)
	_local base_holder << _self.workspace.holder_for(base_col)
	_return _self.activate_follow_root_dialog(holder, base_holder, target_col)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.activate_follow_root_dialog(holder, record_holder,
							      _optional target_col)
	dia << root_dialog.new_dialog(_self)
	dia.set_holder(holder, record_holder, _self.dataset_manager, target_col)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_follow_join_dialog()
	_local dia << follow_join_dialog.new_dialog(_self)
	dia.set_source_holder(_self.workspace, _self.selected_holder)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit_follow_join_dialog(holder, source_col, join_field_name)
	_local source_holder << _self.workspace.holder_for(source_col)
	_local dia << join_dialog.new_dialog(_self)
	dia.set_holder(_self.workspace,
		       holder, source_holder, join_field_name)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_follow_method_dialog()
	_return _self.do_activate_follow_method_dialog(_unset, _self.selected_top_holder)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit_follow_method_dialog(holder,
						   base_col, methd, args)
	
	_local base_holder << _self.workspace.holder_for(base_col)
	_return _self.do_activate_follow_method_dialog(holder, base_holder, methd, args)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.do_activate_follow_method_dialog(holder, record_holder, _optional methd, args)
	_local dia << method_dialog.new_dialog(_self)
	dia.set_holder(holder, record_holder, _self.dataset_manager,
		       methd, args)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_spatial_filter_dialog()
	_local dia << spatial_filter_dialog.new_dialog(_self)
	dia.set_source_holder(_self.workspace, _self.selected_holder)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit_spatial_filter_dialog(holder, source_col, source_geom_name,
						    operator,
						    filter_col, filter_geom_name)
	_local source_holder << _self.workspace.holder_for(source_col)
	_local filter_holder << _self.workspace.holder_for(filter_col)
	
	_local dia << spatial_filter_dialog.new_dialog(_self)
	dia.set_holder(_self.workspace,
		       holder, source_holder, source_geom_name,
		       operator, filter_holder, filter_geom_name)
	dia.activate()
	_return dia
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_distance_filter_dialog()
	_local dia << distance_filter_dialog.new_dialog(_self)
	dia.set_source_holder(_self.workspace, _self.selected_holder)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit_distance_filter_dialog(holder, source_col, source_geom_name,
						    distance, inverse?,
						    filter_col, filter_geom_name)
	_local source_holder << _self.workspace.holder_for(source_col)
	_local filter_holder << _self.workspace.holder_for(filter_col)
	
	_local dia << distance_filter_dialog.new_dialog(_self)
	dia.set_holder(_self.workspace,
		       holder, source_holder, source_geom_name,
		       distance, inverse?, filter_holder, filter_geom_name)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.create_predicate_filter_dialog()
	_local dia << predicate_filter_dialog.new_dialog(_self)
	dia.set_source_holder(_self.workspace, _self.selected_holder)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.edit_predicate_filter_dialog(holder, base_col, predicate)
	_local record_holder << _self.workspace.holder_for(base_col)
	_return _self.do_create_predicate_filter_dialog(holder, record_holder, predicate)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.do_create_predicate_filter_dialog(holder, record_holder, _optional predicate)
	_local dia << predicate_filter_dialog.new_dialog(_self)
	dia.set_holder(_self.workspace, holder, record_holder, predicate)
	dia.activate()
	_return dia
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.selected_col_and_spatial()
	_local holders << _self.selected_top_holders
	_local holder, geom
	_for i_holder _over holders.fast_elements()
	_loop
		_if i_holder.is_record_collection?
		_then
			holder << i_holder
		_elif i_holder.is_spatial? _orif
		      i_holder.is_area?
		_then
			geom << i_holder
		_endif
	_endloop
	_return holder, geom
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.selected_col_and_col()
	_local holders << _self.selected_top_holders
	_local holder1, holder2
	_for i_holder _over holders.fast_elements()
	_loop
		_if i_holder.is_record_collection?
		_then
			_if holder1 _is _unset
			_then
				holder1 << i_holder
			_elif holder2 _is _unset
			_then
				holder2 << i_holder
			_endif
		_endif
	_endloop
	_return holder1, holder2
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.selected_spatial_and_spatial()
	_local holders << _self.selected_top_holders
	_local holder1, holder2
	_for i_holder _over holders.fast_elements()
	_loop
		_if i_holder.is_spatial? _orif
		    i_holder.is_area?
		_then
			_if holder1 _is _unset
			_then
				holder1 << i_holder
			_elif holder2 _is _unset
			_then
				holder2 << i_holder
			_endif
		_endif
	_endloop
	_return holder1, holder2
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.selected_spatial()
	_return _self.selected_spatial_and_spatial()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.dataset_manager
	_return .owner.dataset_manager
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.ace_control
	_return .owner.ace_control
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.ace
	_return .owner.ace
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.note_workspace_change(renew?)
	_self.manage_actions()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.note_selection_change()
	_self.manage_actions()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.note_map_selection_change(data)
	_self.manage_actions()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.note_map_record_selection_change(data)
	_local (pl, rset) << (_scatter data)
	
	_if pl.is_kind_of?(explorer_plugin)
	_then
		_self.manage_actions()
	_endif 
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.note_map_trail_change(data)
	_self.manage_actions()
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.has_trail?
	>> _self.owner.map_trail.empty?.not
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.has_map_selection?
	>> _self.owner.map_selection.empty?.not 
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method construct_dialog.has_explorer_selection?
	_local data << .owner.explorer_selection
	
	_if data _is _unset _then _return _false _endif
	
	_local (plug, sel) << (_scatter data)
	
	>> sel.is_records?
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method construct_dialog.manage_actions()
	_local holders << _self.selected_top_holders
	_local holder << _if holders.size = 1
			 _then >> holders.an_element()
			 _else >> _unset
			 _endif 
	_local any_sel? << _self.selected_holders.empty?.not
	_local any_top_sel? << holder _isnt _unset
	_local record? << holder _isnt _unset _andif holder.is_record_collection?
	_local open? << _self.open?
	.owner.action(:add_collections).enabled? << open?
	.owner.action(:add_trail).enabled? << open? _andif _self.has_trail?
	.owner.action(:add_geom_selection).enabled? << open? _andif _self.has_map_selection?
	.owner.action(:add_collection_selection).enabled? << open? _andif _self.has_map_selection?
	.owner.action(:add_explorer_content).enabled? << open?
	.owner.action(:add_explorer_selection).enabled? << open? _andif _self.has_explorer_selection?
	.owner.action(:add_scrapbook).enabled? << open?
	.owner.action(:spatial_filter).enabled? << open? _andif any_top_sel?
	.owner.action(:distance_filter).enabled? << open? _andif any_top_sel?
	.owner.action(:predicate_filter).enabled? << open? _andif any_top_sel? _andif record?

	.owner.action(:follow_topology).enabled? << open? _andif record?
	.owner.action(:follow_trace).enabled? << open? _andif record?
	.owner.action(:follow_join).enabled? << open? _andif record?
	.owner.action(:follow_method).enabled? << open?
	.owner.action(:follow_root).enabled? << open?

	.owner.action(:create_spatial).enabled? << open?
	.owner.action(:create_buffer).enabled? << open?

	.owner.action(:intersection).enabled? << open?
	.owner.action(:subtraction).enabled? << open?
	.owner.action(:union).enabled? << open?
	.owner.action(:xor).enabled? << open?

	.owner.action(:view_by_join).enabled? << open?
	.owner.action(:view_by_eq).enabled? << open?
	.owner.action(:materialize).enabled? << open?
	.owner.action(:materialize_spatial).enabled? << open?

	.owner.action(:edit).enabled? << open? _andif any_top_sel? _andif holder.editable?
	.owner.action(:view).enabled? << open? _andif any_top_sel? _andif holder.editable?
	.owner.action(:delete).enabled? << open? _andif any_top_sel?
	.owner.action(:move_up).enabled? << open?  _andif any_top_sel?   _andif _self.can_move_up?
	.owner.action(:move_down).enabled? << open?  _andif any_top_sel? _andif _self.can_move_down?
	.owner.action(:copy).enabled? << open? _andif any_sel?
_endmethod
$
