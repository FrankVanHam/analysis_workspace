#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:background_runner)

_pragma(classify_level=advanced, topic={analysis})
##
## Simple helper object that runs methods in the background will
## letting a counter run to show the amount of time elapsed.
##
def_slotted_exemplar(:background_runner,
	{
		{:thread, _unset},
		{:busy_label, _unset}
	},
	:model)
$

message_handler.new(:background_runner).add_uses(:model, :gen_graphics)
$

_pragma(classify_level=advanced, topic={analysis})
_method background_runner.new(busy_label)
	_return _clone.init(busy_label)	
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method background_runner.init(busy_label)
	.busy_label << busy_label
	
	_super.init()
	_self.update_initial_labels()
	_return  _self 
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_private _method background_runner.update_initial_labels()
	.busy_label.label << _self.message(:not_busy)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method background_runner.cancel_run()
	_if .thread _isnt _unset
	_then
	     .thread.kill(:kill)
	     .thread << _unset
	     .busy_label.label << _self.message(:interrupted)
	_endif 
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method background_runner.run_in_background(receiver, meth, args, descr)
	_if .thread _isnt _unset
	_then
		.thread.kill(:kill)
	_endif
	.thread << _proc(me,receiver,  meth, args, descr)
			   me.perform(:|running_in_background()|, receiver, meth, args, descr)
		   _endproc.fork_at(3, _self, receiver, meth, args, descr)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method background_runner.running_in_background(receiver, meth, args, descr)
	_self.changed(:start_running)
	.busy_label.label << _self.message(:running_with, _unset, descr)
	_local start_time << system.elapsed_seconds()
	_protect
		receiver.perform(meth, _scatter args)
	_protection
		.thread << _unset
		.busy_label.label << _self.message(:finished_with, _unset, descr)
		_self.changed(:ended_running)
	_endprotect
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method background_runner.running?
	_return .thread _isnt _unset 
_endmethod
$
