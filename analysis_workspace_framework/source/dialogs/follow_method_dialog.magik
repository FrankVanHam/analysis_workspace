#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:follow_method_dialog)
$

_pragma(classify_level=restricted, topic={analysis})
##
## Dialog to select a method that will be used to create a
## method_collection. The developer should have created a
## method named :rwan_methods on the exemplar to make this
## work. 
##
def_slotted_exemplar(:follow_method_dialog,
	{
		{:target_table,  _unset},
		{:methd,         _unset}
	},
	:base_dialog)
$

message_handler.new(:follow_method_dialog).add_uses(:model, :base_dialog)
$

_pragma(classify_level=restricted, topic={analysis})
_method follow_method_dialog.set_source_holder(workspace, source_holder)
	.workspace << workspace
	.source_holder << source_holder
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method follow_method_dialog.set_holder(workspace,
				      holder, source_holder,
				      methd)
	.workspace << workspace
	.holder << holder
	.source_holder << source_holder
	.name << holder.name
	.methd << methd
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method follow_method_dialog.activate_in(container)
	container.top_frame.modality << :primary_modal
	.items << property_list.new()
	_local rc << rowcol.new(container,_unset, 1, _unset, :style, :top_level,
				:row_alignment, :fill,
				:col_alignment, :fill)
	_self.build_top_label(rc)
	_self.build_method_selection(rc)
	_self.build_name(rc)
	panel_separator.new(rc, :col_alignment, :fill)
	_self.build_buttons(rc)
	_return rc
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method follow_method_dialog.build_method_selection(container)
	_local rc << rowcol.new(container, _unset, 2, _unset, :style, :nested, :col_alignment, :fill)
	
	sw_label_item.new(rc, _self.message(:source_holder))
	sw_text_item.new(rc,
			 :value, .source_holder.name,
			 :enabled?, _false,
			 :col_alignment, :fill)
	
	sw_label_item.new(rc, _self.message(:method))
	.items[:method_choice] << sw_text_item.new(rc, :text_items, {},
						   :editable?, _false,
						   :model, _self,
						   :change_selector, :|method_changed()|,
						   :col_alignment, :fill)
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method follow_method_dialog.on_activation()
	_self.initialise_method()
	_self.initialise_name()
	_super.on_activation()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method follow_method_dialog.initialise_method()
	_local items << _self.method_items
	.items[:method_choice].text_items << items
	_self.set_current_method(items)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method follow_method_dialog.method_changed(methd)
	.methd << methd
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method follow_method_dialog.set_current_method(items)
	_local value << _unset
	_if items.size > 0
	_then
		value << items[1][1]
	_endif 
	_if .methd _isnt _unset
	_then
		_for i_vec _over items.fast_elements()
		_loop
			_if i_vec[1][2] = .methd
			_then
				value << i_vec[1]
			_endif
		_endloop
	_endif 
	_if value _isnt _unset
	_then
		.items[:method_choice].rwan_value_with_notify << value
	_endif 
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method follow_method_dialog.construct_holder()
	_local col << .source_holder.collection.follow_method(.methd)
	_return .parent_model.new_holder(col, .name)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method follow_method_dialog.method_items
	_local items << rope.new()
	_local sw_col << .source_holder.collection.expose()
	_local methods << sw_col.record_exemplar.rwan_methods
	_if methods.empty?
	_then
		items.add( {_unset, _self.message(:no_method)} )
	_else
		_for i_methd, i_vec _over methods.fast_keys_and_elements()
		_loop
			(methd, args, external_name, target) << (_scatter i_vec)
			items.add({i_methd, external_name})
		_endloop
		items << items.as_sorted_collection(_proc(a,b) >> a[2] _cf b[2] _endproc )
		items << rope.new_from(items)
	_endif
	_return items
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method follow_method_dialog.manage_actions()
	_local has_name? << .name.default("") <> ""
	.items[:ok].enabled? << .readonly?.not _andif has_name? _andif (.methd _isnt _unset)
	.items[:name].enabled? << .readonly?.not _andif (.methd _isnt _unset)
_endmethod
$
