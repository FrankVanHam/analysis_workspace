#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:record_subtraction_dialog)
$

_pragma(classify_level=restricted, topic={analysis})
##
## Dialog to construct a substraction_collection that will
## substract one collection from the other
##
def_slotted_exemplar(:record_subtraction_dialog,
	{
		{:items, _unset},
		{:holder1, _unset},
		{:holder2, _unset}
	},
	:model)
$

message_handler.new(:record_subtraction_dialog).add_uses(:model, :gen_graphics)
$

_pragma(classify_level=restricted, topic={analysis})
_method record_subtraction_dialog.set_holders(holder1, holder2)
	.holder1 << holder1
	.holder2 << holder2
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method record_subtraction_dialog.activate_in(container)
	container.modality << :primary_modal
	.items << property_list.new()
	_local rc << rowcol.new(container,_unset, 1, _unset, :style, :top_level, :resize?, _true, :row_resize_values, {100})
	_self.build_top_label(rc)
	_self.build_collection_selection(rc)
	panel_separator.new(rc, :col_alignment, :fill)
	_self.build_buttons(rc)
	_return rc
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_private _method record_subtraction_dialog.build_top_label(container)
	text_window.new(container, 4, 50, _unset,
			:horizontal_scrollbar?, _false,
			:editable?, _false,
			:contents, _self.message(:top_label),
			:col_alignment, :fill,
			:row_alignment, :fill )
_endmethod
$



_pragma(classify_level=restricted, topic={analysis})
_private _method record_subtraction_dialog.build_collection_selection(container)
	_local rc << rowcol.new(container, 1, _unset, _unset, :style, :nested, :col_alignment, :fill)
	.items[:col1] << label_item.new(rc, _unset, :col_alignment, :fill)
	image_item.new(rc, smallworld_product.get_raster_image(:remove, _self.module_name))
	.items[:col2] << label_item.new(rc, _unset, :col_alignment, :fill)
	_self.update_labels()
	button_item.new_safe(rc, _self.message(:swap), _self, :|swap()|, :min_width, 75)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method record_subtraction_dialog.update_labels()
	 .items[:col1].label << .holder1.name
	 .items[:col2].label << .holder2.name
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method record_subtraction_dialog.swap()
	(.holder1, .holder2) << (.holder2, .holder1)
	_self.update_labels()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method record_subtraction_dialog.build_buttons(container)
	## 
	_local rc << rowcol.new(container, 1, _unset, _unset, :style, :button_box, :col_alignment, :right)
	.items[:ok]     << button_item.new_safe(rc, _self.message(:ok), _self, :|ok()|, :min_width, 75)
	.items[:cancel] << button_item.new_safe(rc, _self.message(:cancel), _self, :|cancel()|, :min_width, 75)
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method record_subtraction_dialog.ok()
	_local new_col << _self.construct_spatial_filter()
	.parent_model.add_collection(new_col)
	_self.close()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method record_subtraction_dialog.construct_spatial_filter()
	_local new_col << .holder1.collection.subtraction(.holder2.collection)
	_return new_col
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method record_subtraction_dialog.cancel()
	## 
	## 
	_self.close()
_endmethod
$


