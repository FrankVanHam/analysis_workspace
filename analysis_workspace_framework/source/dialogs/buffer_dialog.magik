#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:buffer_dialog)
$

_pragma(classify_level=basic, topic={analysis})
## Dialog that creates a buffer around the base collection. The
## collection can either be a record_collection or a geometry
## collection. 
def_slotted_exemplar(:buffer_dialog,
	{
		{:items, _unset},
		{:holder, _unset}
	},
	:model)
$

_pragma(classify_level=basic, topic={analysis})
message_handler.new(:buffer_dialog).add_uses(:model, :gen_graphics)
$


_pragma(classify_level=restricted, topic={analysis})
_method buffer_dialog.set_holder(holder)
	.holder << holder
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method buffer_dialog.activate_in(container)
	container.modality << :primary_modal
	.items << property_list.new()
	_local rc << rowcol.new(container,_unset, 1, _unset, :style, :top_level, :resize?, _true, :row_resize_values, {0,100,0,0})
	_self.build_top_label(rc)
	_self.build_buffer_options(rc)
	panel_separator.new(rc, :col_alignment, :fill)
	_self.build_buttons(rc)
	_return rc
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.build_top_label(container)
	text_window.new(container, 7, 50, _unset,
			:horizontal_scrollbar?, _false,
			:editable?, _false,
			:contents, _self.message(:top_label),
			:col_alignment, :fill,
			:row_alignment, :fill )
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.build_buffer_options(container)
	_local rc << rowcol.new(container, _unset, 2, _unset, :style, :nested, :col_alignment, :fill)
	label_item.new(rc, _self.message(:source_collection))
	.items[:col] << label_item.new(rc, .holder.name, :col_alignment, :fill)
	
	label_item.new(rc, _self.message(:geometry))
	.items[:geometry_choice] << text_choice_item.new(rc, _unset, :items, {}, :col_alignment, :fill)
	
	label_item.new(rc, _self.message(:distance))
	.items[:distance] << ti << managed_value_text_item.new(rc, _unset, :value, 10, :display_length, 10, :col_alignment, :fill)
	ti.manager << unit_value_manager.new(unit_manager.unit(:m))
	
	label_item.new(rc, _self.message(:corner_style))
	.items[:corner_style] << image_choice_item.new(rc, _unset, :items, _self.corner_style_items)
	.items[:corner_style].col_alignment << :fill
	
	label_item.new(rc, _self.message(:end_style))
	.items[:end_style] << image_choice_item.new(rc, _unset, :items, _self.end_style_items)
	.items[:end_style].col_alignment << :fill
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.build_buttons(container)
	_local rc << rowcol.new(container, 1, _unset, _unset, :style, :button_box, :col_alignment, :right)
	.items[:ok]     << button_item.new_safe(rc, _self.message(:ok), _self, :|ok()|, :min_width, 75)
	.items[:cancel] << button_item.new_safe(rc, _self.message(:cancel), _self, :|cancel()|, :min_width, 75)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method buffer_dialog.on_activation()
	.items[:corner_style].value << :circular
	.items[:end_style].value << :circular
	_self.initialise_geometry_choice()
	_self.manage_actions()
	_return _super.on_activation()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.initialise_geometry_choice()
	_local choice << .items[:geometry_choice]
	choice.items << items << _self.geom_field_items
	choice.anco_value_with_notify << _self.most_likely_geom_field(items)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.most_likely_geom_field(items)
	_if items.size = 1
	_then
		_return items[1][1]
	_endif 
	_local _constant topo_cats << {:chain, :point, :area}
	_for i_vec _over items.fast_elements()
	_loop
		_local geom_cat << i_vec[1].geom_category
		_if topo_cats.includes?(geom_cat)
		_then
			_return i_vec[1]
		_endif
	_endloop
	_return items[1][1]	 
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method buffer_dialog.manage_actions()
	_local col? << .holder.is_record_collection?
	.items[:col].enabled? << col?
	.items[:geometry_choice].enabled? << col?
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method buffer_dialog.ok()
	_local new_col << _self.construct_buffer_filter()
	.parent_model.add_collection(new_col)
	_self.close()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.construct_buffer_filter()
	## 
	##
	_local corner_style << .items[:corner_style].value
	_local end_style << .items[:end_style].value
	_local distance << .items[:distance].value
	_local fd << .items[:geometry_choice].value
	_if fd _isnt _unset
	_then
		col_geom << fd.name
	_else
		col_geom << _unset
	_endif 
	_local new_col << .holder.collection.buffer(col_geom, distance, corner_style, end_style)
	_return new_col
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method buffer_dialog.cancel()
	_self.close()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.geom_field_items
	_return _self.geom_field_items_for(.holder.collection)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.geom_field_items_for(col)
	_local items << rope.new()
	_if .holder.is_record_collection?
	_then 
		_local sw_col << col.expose()
		_for i_fd _over sw_col.descriptor.geometry_fields()
		_loop
			_if i_fd.name _is :meatball _then _continue _endif 
			items.add( {i_fd, i_fd.external_name} )
		_endloop
		items << items.as_sorted_collection(_proc(a,b) >> a[2] _cf b[2] _endproc )
		items << rope.new_from(items)
	_else
		items.add({_unset, _self.message(:geometry_collection)})
	_endif 
	_return items
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.corner_style_items
	_local styles << {:circular, :bevelled, :square, :acute}
	_local items << rope.new()
	_for i_style _over styles.fast_elements()
	_loop
		_local img << smallworld_product.get_raster_image(:corner_style_+i_style,
								  _self.module_name)
		items.add( {i_style, img})
	_endloop
	_return items
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method buffer_dialog.end_style_items
	_local styles << {:circular, :square, :truncate}
	_local items << rope.new()
	_for i_style _over styles.fast_elements()
	_loop
		_local img << smallworld_product.get_raster_image(:end_style_+i_style,
								  _self.module_name)
		items.add({i_style, img})
	_endloop
	_return items
_endmethod
$


