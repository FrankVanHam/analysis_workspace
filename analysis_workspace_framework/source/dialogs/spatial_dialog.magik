#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:spatial_dialog)
$

_pragma(classify_level=restricted, topic={analysis})
##
## Dialog to create a spatial_collection of a base
## collection. This involves selecting a geometry field to follow.
##
def_slotted_exemplar(:spatial_dialog,
	{
		{:items, _unset},
		{:holder, _unset}
	},
	:model)
$

message_handler.new(:spatial_dialog).add_uses(:model, :gen_graphics)
$


_pragma(classify_level=restricted, topic={analysis})
_method spatial_dialog.set_holder(holder)
	.holder << holder
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method spatial_dialog.activate_in(container)
	container.modality << :primary_modal
	.items << property_list.new()
	_local rc << rowcol.new(container,_unset, 1, _unset, :style, :top_level, :resize?, _true, :row_resize_values, {100})
	_self.build_top_label(rc)
	_self.build_geometry_selection(rc)
	panel_separator.new(rc, :col_alignment, :fill)
	_self.build_buttons(rc)
	_return rc
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_dialog.build_top_label(container)
	text_window.new(container, 3, 70, _unset,
			:horizontal_scrollbar?, _false,
			:editable?, _false,
			:contents, _self.message(:top_label),
			:col_alignment, :fill,
			:row_alignment, :fill )
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method spatial_dialog.on_activation()
	_self.initialise_geometry_choice()
	_return _super.on_activation()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_dialog.initialise_geometry_choice()
	_local choice << .items[:geometry_choice]
	choice.items << items << _self.geom_field_items
	choice.anco_value_with_notify << _self.most_likely_geom_field(items)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_dialog.most_likely_geom_field(items)
	_if items.size = 1
	_then
		_return items[1][1]
	_endif 
	_local _constant topo_cats << {:chain, :point, :area}
	_for i_vec _over items.fast_elements()
	_loop
		_local geom_cat << i_vec[1].geom_category
		_if topo_cats.includes?(geom_cat)
		_then
			_return i_vec[1]
		_endif
	_endloop
	_return items[1][1]	 
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_dialog.build_geometry_selection(container)
	_local rc << rowcol.new(container, _unset, 2, _unset, :style, :nested, :col_alignment, :fill)
	label_item.new(rc, _self.message(:source_collection))
	label_item.new(rc, .holder.name)
	label_item.new(rc, _self.message(:geometry))
	.items[:geometry_choice] << text_choice_item.new(rc, _unset, :items, {}, :col_alignment, :fill)
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_dialog.build_buttons(container)
	_local rc << rowcol.new(container, 1, _unset, _unset, :style, :button_box, :col_alignment, :right)
	.items[:ok]     << button_item.new_safe(rc, _self.message(:ok), _self, :|ok()|, :min_width, 75)
	.items[:cancel] << button_item.new_safe(rc, _self.message(:cancel), _self, :|cancel()|, :min_width, 75)
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method spatial_dialog.ok()
	_local new_col << _self.construct_spatial_filter()
	.parent_model.add_collection(new_col)
	_self.close()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_dialog.construct_spatial_filter()
	_local col_geom << .items[:geometry_choice].value.name
	_local new_col << .holder.collection.spatial_collection(col_geom)
	_return new_col
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method spatial_dialog.cancel()
	_self.close()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_dialog.geom_field_items
	_return _self.geom_field_items_for(.holder.collection)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_dialog.geom_field_items_for(col)
	_local sw_col << col.expose()
	_local items << rope.new()
	_for i_fd _over sw_col.descriptor.geometry_fields()
	_loop
		_if i_fd.name _is :meatball _then _continue _endif 
		items.add( {i_fd, i_fd.external_name} )
	_endloop
	items << items.as_sorted_collection(_proc(a,b) >> a[2] _cf b[2] _endproc )
	items << rope.new_from(items)
	_return items
_endmethod
$
