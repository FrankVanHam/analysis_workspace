#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:method_dialog)
$

_pragma(classify_level=restricted, topic={analysis})
##
## Dialog to select a method that will be used to create a
## method_collection. The developer should have created a
## method named :rwan_methods on the exemplar to make this
## work. 
##
def_slotted_exemplar(:method_dialog,
	{
		{:items,         _unset},
		{:method_items,  _unset},
		{:holder,        _unset},
		{:record_holder, _unset},
		
		{:target_table,  _unset},
		{:methd,         _unset},
		{:args,          _unset}
	},
	:model)
$

message_handler.new(:method_dialog).add_uses(:model, :gen_graphics)
$

_pragma(classify_level=restricted, topic={analysis})
_method method_dialog.set_holder(holder, record_holder, dsm, methd)
	.holder << holder
	.record_holder << record_holder
	.methd << methd
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method method_dialog.activate_in(container)
	container.top_frame.modality << :primary_modal
	.items << property_list.new()
	_local rc << rowcol.new(container,_unset, 1, _unset, :style, :top_level,
				:row_alignment, :fill,
				:col_alignment, :fill)
	_self.build_top_label(rc)
	_self.build_method_selection(rc)
	panel_separator.new(rc, :col_alignment, :fill)
	_self.build_buttons(rc)
	_return rc
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_private _method method_dialog.build_top_label(container)
	sw_text_window.new(container, 7, 100, _unset,
			:horizontal_scrollbar?, _false,
			:editable?, _false,
			:contents, _self.message(:top_label),
			:col_alignment, :fill)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method method_dialog.build_method_selection(container)
	_local rc << rowcol.new(container, _unset, 2, _unset, :style, :nested, :col_alignment, :fill)
	sw_label_item.new(rc, _self.message(:source))
	sw_label_item.new(rc, .record_holder.name, :col_alignment, :fill)
	
	sw_label_item.new(rc, _self.message(:method))
	.items[:method_choice] << sw_text_item.new(rc, _unset, :text_items, {},
						       :model, _self,
						       :change_selector, :|method_changed()|,
						       :col_alignment, :fill)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method method_dialog.build_buttons(container)
	## 
	_local rc << rowcol.new(container, 1, _unset, _unset, :style, :button_box,
				:row_alignment, :fill,
				:col_alignment, :right)
	.items[:ok]     << sw_button_item.new_safe(rc, _self.message(:ok), _self, :|ok()|,
						:row_alignment, :bottom,
						:min_width, 75)
	.items[:cancel] << sw_button_item.new_safe(rc, _self.message(:cancel), _self, :|cancel()|,
						:row_alignment, :bottom,
						:min_width, 75)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method method_dialog.on_activation()
	_local items << _self.method_items
	.items[:method_choice].text_items << items
	_self.set_current_method(items)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method method_dialog.method_changed(vec)
	(.target_table, .methd, .args) << (_scatter vec)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method method_dialog.set_current_method(items)
	_local value << _unset
	_if items.size > 0
	_then
		value << items[1][1]
	_endif 
	_if .methd _isnt _unset
	_then
		_for i_vec _over items.fast_elements()
		_loop
			_if i_vec[1][2] = .methd
			_then
				value << i_vec[1]
			_endif
		_endloop
	_endif 
	_if value _isnt _unset
	_then
		.items[:method_choice].rwan_value_with_notify << value
	_endif 
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method method_dialog.ok()
	_if .holder _is _unset
	_then 
		new_col << .record_holder.collection.follow_method(.target_table, .methd, .args)
		.parent_model.add_collection(new_col)
	_else
		.holder.collection.update(.target_table, .methd, .args)
		.parent_model.update_holder(.holder)
	_endif 
	_self.close()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method method_dialog.cancel()
	_self.close()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method method_dialog.method_items
	_local items << rope.new()
	_local sw_col << .record_holder.collection.expose()
	_local methods << sw_col.record_exemplar.rwan_methods
	_if methods.empty?
	_then
		items.add( {_unset, _self.message(:no_method)} )
	_else
		_for i_vec _over methods.fast_elements()
		_loop
			(methd, args, external_name, target) << (_scatter i_vec)
			items.add({{target, methd, args}, external_name})
		_endloop
		items << items.as_sorted_collection(_proc(a,b) >> a[2] _cf b[2] _endproc )
		items << rope.new_from(items)
	_endif
	_return items
_endmethod
$
