#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:trace_analysis_test_case)
$

_pragma(classify_level=basic, topic={analysis})
def_slotted_exemplar(:trace_analysis_test_case,
	{},
	:base_analysis_test_case)
$

_pragma(classify_level=basic, topic={analysis})
_private _method trace_analysis_test_case.supply_point_basics()
	_local dsm << rwan:analysis_dataset_manager.new(:test)
	_local pcol << rwan:sw_collection.new(dsm, _self.supply_point_collection)
	_local one_col << pcol.filter_by_predicate(predicate.eq(:id, "CAM-909169-SP"))
	
	_local link_table << gis_program_manager.databases[:electricity].collections[:substation_internals_link]
	_return one_col, link_table
_endmethod
$


_pragma(classify_level=basic, topic={analysis})
_method trace_analysis_test_case.test_trace_from_supply_point_to_station()
	_local (one_col, link_table) << _self.supply_point_basics()
	_local trace_col << one_col.follow_trace(:location, link_table, _unset, _unset)
	_self.assert_is(trace_col.size, 1, "There should be 1 substation link in the set")
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method trace_analysis_test_case.test_trace_from_supply_point_to_station_with_predicate_stop()
	_local (one_col, link_table) << _self.supply_point_basics()
	_local stops << property_list.new_with(:joint,{_unset})
	_local trace_col << one_col.follow_trace(:location, link_table, stops, _unset)
	_self.assert_is(trace_col.size, 0, "There should be 0 substation link in the set because the trace stops at a joint")
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method trace_analysis_test_case.test_trace_from_supply_point_to_station_with_predicate_false_stop()
	_local (one_col, link_table) << _self.supply_point_basics()
	_local stops << property_list.new_with(:joint,{predicate.lt(:id,0)})
	_local trace_col << one_col.follow_trace(:location, link_table, stops, _unset)
	_self.assert_is(trace_col.size, 1, "There should be 1 substation link in the set because the stop condition is always false")
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method trace_analysis_test_case.test_trace_from_supply_point_to_station_with_unsufficient_max_length()
	_local (one_col, link_table) << _self.supply_point_basics()
	_local stops << _unset 
	_local trace_col << one_col.follow_trace(:location, link_table, stops, length_value(1000,:mm))
	_self.assert_is(trace_col.size, 0, "There should be 0 substation link in the set because the max length is too little")
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method trace_analysis_test_case.test_trace_from_supply_point_to_station_with_sufficient_max_length()
	_local (one_col, link_table) << _self.supply_point_basics()
	_local stops << _unset 
	_local trace_col << one_col.follow_trace(:location, link_table, stops, length_value(100000,:mm))
	_self.assert_is(trace_col.size, 1, "There should be 1 substation link in the set because the max length is enough")
_endmethod
$
