#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:binary_op_geometry_col_test_case)

_pragma(classify_level=basic, topic={analysis})
##
## Test distance filters.
##
def_slotted_exemplar(:distance_filter_geometry_col_tc,
	{},
	:base_analysis_db_test_case)
$

_pragma(classify_level=basic, topic={analysis})
_method distance_filter_geometry_col_tc.test_geom_category
	_local dsm << rwan:analysis_dataset_manager.new(:test)
	_local area_col << _self.area_collection(dsm, bounding_box.new_with_size(0,0,1000,1000).coords)
	_local area_col2 << _self.area_collection(dsm, bounding_box.new_with_size(500,500,1000,1000).coords)
	_local filter_col << area_col.filter_by_distance(:geometry, length_value(10,:m), _false, area_col2, :geometry)

	_self.assert_equals(1, filter_col.size, "Should be 1")
	_self.assert_equals(:area, filter_col.geom_category, "Areas")

	area_col2 << _self.area_collection(dsm, bounding_box.new_with_size(5000,5000,1000,1000).coords)
	 filter_col << area_col.filter_by_distance(:geometry, length_value(10,:mm), _false, area_col2, :geometry)
	_self.assert_equals(0, filter_col.size, "Should be 0")
	_self.assert_equals(:area, filter_col.geom_category, "Areas")
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method distance_filter_geometry_col_tc.test_basic()
	## Test includes?() and an_element() and size
	## 
	_local dsm  << rwan:analysis_dataset_manager.new(:test)
		
	_local s1 << _self.sector_rope_from(bounding_box.new_with_size(0,100, 1000,1000).coords)
	_local s2 << _self.sector_rope_from(bounding_box.new_with_size(1000,1000, 1000,1000).coords)
	_local s3 << _self.sector_rope_from(bounding_box.new_with_size(2000,100, 1000,1000).coords)
	
	_local a1 << pseudo_area.new_for_world(s1, _self.gv.world)
	_local a2 << pseudo_area.new_for_world(s2, _self.gv.world)
	_local a3 << pseudo_area.new_for_world(s3, _self.gv.world)
	
	_local park_col << rwan:geometry_collection.new(dsm, geometry_set.new_with(a1,a2,a3))
	
	_local line_col << _self.lines_collection(dsm, {{0,0,3000,0}})
	_local fcol << park_col.filter_by_distance(:coverage, length_value(200,:mm), _false, line_col, :geometry)
	
	_self.assert_true(fcol.ref_collections.includes?(park_col), "Road is the base")
	_self.assert_true(fcol.includes?(a1), "A1 should be includes")
	_self.assert_false(fcol.includes?(a2), "A2 should not be includes")
	
	_local an << fcol.an_element()
	_self.assert_true((a1 = an) _orif (a3 = an), "Should be one of 2")
	_self.assert_equals(2, fcol.size, "Size = 2")
	
	# inverse if
	_local b_col << park_col.filter_by_bounds(:coverage, bounding_box.new_with_size(0,0,3000,1000))
	fcol << b_col.filter_by_distance(:coverage, length_value(200,:mm), _true, line_col, :geometry)
	_self.assert_true(fcol.includes?(a2), "A2 should be includes")
	_self.assert_false(fcol.includes?(a1), "A1 should not be includes")
	
	an << fcol.an_element()
	_self.assert_true((a2 = an), "Should be one really")
	_self.assert_equals(1, fcol.size, "Size = 1")
_endmethod
$
