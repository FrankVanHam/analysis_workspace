#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:root_collection)

_pragma(classify_level=basic, topic={analysis})
##
## Analysis collections that will yield one of the base
## collections that self was derived from.
## For example: say you start with a road, follow the topology
## to a hotel, filter on a type of hotel, then an additional
## rot collection can yield the roads that lead to the hotels
## you required.
## 
def_slotted_exemplar(:root_collection,
	{},
	:base_follow_collection)
$

_pragma(classify_level=basic, topic={analysis})
_method root_collection.new(dsm, base_col, target_col)
	_return _clone.init(dsm, base_col, target_col)
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method root_collection.editable?
	_return _true 
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method root_collection.activate_editor_in(dialog, holder)
	_return dialog.edit_follow_root_dialog(holder,
					       _self.base_collection,
					       _self.target_collection)
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method root_collection.update(target_col)
	.target_ds_name << target_col.dataset.original_dataset_name
	.target_table_name << target_col.name
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_iter _method root_collection.fast_elements()
	_dynamic !root_query! << hash_table.new()
	_local done << large_record_register.new(100)
	_for i_rec _over _self.base_collection.fast_elements()
	_loop
		_local root << !root_query![.target_table_name]
		
		_if root _isnt _unset _andif
		    _not done.includes?(root)
		_then
			_loopbody(root)
			done.add(root)
		_endif 
	_endloop
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method root_collection.description(translator)
	_local base_name << translator.name_for_collection(_self.base_collection)
	_return _self.message(:description, _self.target_collection.external_name,
			      base_name)
_endmethod
$
