#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$


#remex(:base_follow_collection)
$

_pragma(classify_level=basic, topic={analysis})
##
## Base class for all classes that follow some path towards
## another collection.
def_slotted_exemplar(:base_follow_collection,
	{
		{:target_ds_name,    _unset},
		{:target_table_name, _unset}
	},
	:base_record_collection)
$


_pragma(classify_level=basic, topic={analysis})
_method base_follow_collection.new()
	condition.raise(:error, :string, "Should not be invoked")
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_private _method base_follow_collection.init(dsm, base_col, target_col)
	.target_ds_name << target_col.dataset.original_dataset_name
	.target_table_name << target_col.name
	_return _super.init(dsm, base_col) 
_endmethod
$


_pragma(classify_level=restricted, topic={analysis})
_method base_follow_collection.serial_slots()
	_local (keys, values) << _super.serial_slots()
	keys.add_all_last({:target_ds_name, :target_table_name})
	values.add_all_last({.target_ds_name, .target_table_name})
	_return keys, values	
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method base_follow_collection.init_from_serial(dsm, base_cols, target_ds_name, target_table_name)
    _super.init_from_serial(dsm, base_cols)
    .target_ds_name << target_ds_name
    .target_table_name << target_table_name
    _return _self 
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method base_follow_collection.target_collection
	_return gis_program_manager.databases[.target_ds_name].collections[.target_table_name]
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method base_follow_collection.equals?(other)
	_if _not _self.is_class_of?(other) _then _return _false _endif
	_return other.equals_in_details?(.base_collections, .target_ds_name, .target_table_name)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method base_follow_collection.equals_in_details?(base_cols, target_ds_name, target_table_name)
	_return _super.equals_in_details?(base_cols) _andif
		.target_ds_name = target_ds_name _andif
		.target_table_name = target_table_name
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method base_follow_collection.expose()
	_return _self.target_collection
_endmethod
$
