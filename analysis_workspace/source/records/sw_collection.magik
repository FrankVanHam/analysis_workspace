#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:sw_collection)
$

_pragma(classify_level=basic, topic={analysis})
##
## A collection that is based on a actual smallworld collection.
def_slotted_exemplar(:sw_collection,
	{
		{:actual_ds_name, _unset}
	}, :base_source_collection)
$


_pragma(classify_level=basic, topic={analysis})
_method sw_collection.new(dsm, actual_col)
	_return _clone.init(dsm, actual_col)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method sw_collection.init(dsm, actual_col)
	.actual_ds_name << actual_col.dataset.original_dataset_name
	_return _super.init(dsm, _unset, actual_col.name)
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method sw_collection.origin_is_sw_collection?
	_return _true 
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method sw_collection.is_sw_collection?
	_return _true 
_endmethod
$

_pragma(classify_level=restricted, topic={demo})
_method sw_collection.serial_slots()
	_local (keys, values) << _super.serial_slots()
	keys.add_all_last({:actual_ds_name})
	values.add_all_last({.actual_ds_name})
	_return keys, values
_endmethod
$

_pragma(classify_level=restricted, topic={demo})
_private _method sw_collection.init_from_serial(dsm, base_cols, actual_table_name, actual_ds_name)
	_super.init_from_serial(dsm, base_cols, actual_table_name)
	.actual_ds_name << actual_ds_name
	_return _self 
_endmethod
$

_pragma(classify_level=basic, topic={demo})
_method sw_collection.equals?(other)
	_return _self.is_class_of?(other) _andif 
		other.equals_in_details?(.base_collections, .actual_ds_name, .actual_table_name)
_endmethod
$

_pragma(classify_level=restricted, topic={demo})
_method sw_collection.equals_in_details?(base_cols, actual_ds_name, actual_table_name)
	_if _not _super.equals_in_details?(base_cols, actual_table_name) _then _return _false _endif
	_return .actual_ds_name = actual_ds_name
_endmethod
$

_pragma(classify_level=basic, topic={demo})
_method sw_collection.actual_collection
	_return gis_program_manager.databases[.actual_ds_name].collections[.actual_table_name]
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method sw_collection.size_is_fast?
	_return _self.actual_collection.size_is_fast?
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method sw_collection.size
	_return _self.actual_collection.size
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method sw_collection.description(translator)
	_return _self.actual_collection.external_name
_endmethod
$
