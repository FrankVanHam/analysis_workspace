#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:spatial_filter_collection)

_pragma(classify_level=basic, topic={analysis})
##
## Collection that performs a spatial operator on a geometry
## field of the base collection.
def_slotted_exemplar(:spatial_filter_collection,
	{
		{:source_geom_name,  _unset},
		{:operator,          _unset},
		{:filter_collection, _unset},
		{:filter_geom_name,  _unset}
	}, :base_filter_collection)
$

_pragma(classify_level=basic, topic={analysis})
_method spatial_filter_collection.new(dsm, source_col, source_geom_name, operator, filter_col, filter_geom_name)
	_return _clone.init(dsm, source_col, source_geom_name, operator, filter_col, filter_geom_name)
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_private _method spatial_filter_collection.init(dsm, source_col, source_geom_name, operator, filter_col, filter_geom_name)
	.source_geom_name << source_geom_name
	.operator << operator
	.filter_collection << filter_col
	.filter_geom_name << filter_geom_name

	# protect agains nonsense
	source_col.assert_geom_name(.source_geom_name)
	.filter_collection.assert_geom_name(.filter_geom_name)

	_return _super.init(dsm, source_col)
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method spatial_filter_collection.activate_editor_in(dialog, holder)
	_return dialog.edit_spatial_filter_dialog(holder,
						  .source_collection,
						  .source_geom_name, .operator,
						  .filter_collection, .filter_geom_name)
_endmethod
$


###################################################################################################################

_pragma(classify_level=basic, topic={analysis})
_method spatial_filter_collection.editable?
	_return _true 
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_method spatial_filter_collection.ref_collections
	## return the collections I depend on
	_return {.source_collection, .filter_collection}
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method spatial_filter_collection.filter_stream_for_actual_collection(actual_collection)
	_local filter_gs << .filter_collection.geometry_set(.filter_geom_name)

	# limit the source by the filter bounds to only look in that
	# bounds. Except for disjoint operation...
	_local bounds << _if .operator _isnt :disjoint_from
			 _then >> filter_gs.bounds
			 _else >> _unset 
			 _endif 
	_local source_stream << _self.source_rwo_stream(actual_collection, bounds)
	_return rwan:spatial_filter_read_stream.new(.source_collection, source_stream, .source_geom_name, .operator, filter_gs)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method spatial_filter_collection.filter_stream(stream)
	_local filter_gs << .filter_collection.geometry_set(.filter_geom_name)
	_return rwan:spatial_filter_read_stream.new(.source_collection, stream, .source_geom_name, .operator, filter_gs)
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_filter_collection.source_rwo_stream(actual_collection, bounds)
	_local source_gs << _self.source_geometry_set(actual_collection, bounds)
	_return gs_rwo_set.new(actual_collection.dataset,
			       property_list.new_with(
				       :data_source, source_gs)).read_stream()
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method spatial_filter_collection.source_geometry_set(actual_collection, bounds)
	# if the operator is restricting to the filter, then set the
	# bounds.
	_local world << actual_collection.record_exemplar.rwan_world_for(.source_geom_name)
	_return actual_collection.geometry_set(world, bounds, .source_geom_name)
_endmethod
$



#_pragma(classify_level=basic, topic={analysis, collections})
#_method spatial_filter_collection.size
#	_local geom << _self.need_geometry(.arg)
#	_return _self.source_collection.size_filtered_by_geometry(.source_geom_name, .operator, geom)
#_endmethod
#$
#
#_pragma(classify_level=basic, topic={analysis, collections})
#_method spatial_filter_collection.includes?(rec)
#	_local geom << rec.geometry(.source_geom_name)
#	_if geom _is _unset
#	_then
#		_return _false
#	_endif
#	_local rec_gs << geometry_set.new_with(geom)
#	_local geom_arg << _self.need_geometry(.arg)
#	_return rec_gs.select(.operator, geom_arg).empty?.not
#_endmethod
#$
#
#
#_pragma(classify_level=basic, topic={analysis, collections})
#_iter _method spatial_filter_collection.fast_elements()
#	_local geom << _self.need_geometry(.arg)
#	_for i_rec _over _self.source_collection.fast_elements_filtered_by_geometry(.source_geom_name, .operator, geom)
#	_loop
#		_loopbody(i_rec)
#	_endloop
#_endmethod
#$
#
#_pragma(classify_level=basic, topic={analysis, collections})
#_method spatial_filter_collection.convert_to_geometry()
#	_local geom << _self.need_geometry(.arg)
#	_return _self.source_collection.convert_to_geometry_filtered_by_geometry(.source_geom_name, .operator, geom)
#_endmethod
#$
#
#
#_pragma(classify_level=restricted, topic={analysis})
#_private _method spatial_filter_collection.need_geometry(arg)
#	_return arg.convert_to_geometry()
#_endmethod
#$
#
#
#_pragma(classify_level=basic, topic={analysis})
#_method spatial_filter_collection.description(translator)
#	_local base_name << translator.name_for_collection(_self.source_collection)
#	_local target_name << translator.name_for_collection(.arg)
#	_return _self.message(:description, base_name, .source_geom_name,
#			      .operator.write_string, target_name)
#_endmethod
#$


_pragma(classify_level=advanced, topic={analysis})
_method spatial_filter_collection.display_tree_in(owner)
	_return owner.display_tree_for_spatial_filter_collection(_self, .source_collection, .source_geom_name, .operator,
								 .filter_collection, .filter_geom_name)
_endmethod
$

_pragma(classify_level=advanced, topic={analysis})
_method spatial_filter_collection.discard()
	.source_collection << .source_geom_name << .operator << .filter_collection << .filter_geom_name << _unset 
	_super.discard()
_endmethod
$
