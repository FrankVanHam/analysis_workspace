#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:bounds_filter_geometry_collection)

_pragma(classify_level=basic, topic={analysis})
##
## Collection that filters the geometry collection within the bounds.
##
def_slotted_exemplar(:bounds_filter_geometry_collection,
	{
		{:source_collection, _unset},
		{:bounds,            _unset}
	}, :base_spatial_collection)
$

message_handler.new(:bounds_filter_geometry_collection).add_use(:base_spatial_collection)
$

_pragma(classify_level=basic, topic={analysis})
_method bounds_filter_geometry_collection.new(dsm, source_col, bounds)
	_return _clone.init(dsm, source_col, bounds)
_endmethod
$

_pragma(classify_level=basic, topic={analysis})
_private _method bounds_filter_geometry_collection.init(dsm, source_col, bounds)
	.source_collection << source_col
	.bounds << bounds
	
	_return _super.init(dsm)
_endmethod
$

###################################################################################################################

_pragma(classify_level=basic, topic={analysis})
_method bounds_filter_geometry_collection.editable?
	_return _false 
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method bounds_filter_geometry_collection.size_is_fast?
	## no, size is generally not fast as it is spatially filtered.
	_return _false 
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method bounds_filter_geometry_collection.read_stream(_optional filter)
	## Return my read_stream. If filter is set, the let the filter
	## filter my results. Otherwise set myself as the filter of my
	## source_collection
	#
	_local str << .source_collection.read_stream(_self)
	_if filter _isnt _unset
	_then
		_return filter.filter_stream(str)
	_else 
		_return str
	_endif 
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method bounds_filter_geometry_collection.geometry_for(rec, geom_name)
	_return .source_collection.geometry_for(rec, geom_name)	
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_method bounds_filter_geometry_collection.filter_stream(stream)
	_return bounds_filter_read_stream.new(.source_collection, stream, :geometry, .bounds)
_endmethod
$


_pragma(classify_level=advanced, topic={analysis})
_method bounds_filter_geometry_collection.discard()
	.source_collection << .bounds << _unset 
	_super.discard()
_endmethod
$
