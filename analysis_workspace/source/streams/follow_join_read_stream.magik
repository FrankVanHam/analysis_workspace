#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:follow_join_read_stream)
$

_pragma(classify_level=basic, topic={analysis, collections})
def_slotted_exemplar(:follow_join_read_stream,
	{
		{:join_field_name,  _unset},
		{:multi?,           _unset}
	}, :follow_base_trace_read_stream)
$

_pragma(classify_level=basic, topic={analysis, collections})
_method follow_join_read_stream.new(source_col, join_field_name)
	_return _clone.init(source_col, join_field_name)
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_private _method follow_join_read_stream.init(source_col, join_field_name)
	.join_field_name << join_field_name
	_return _super.init(source_col)
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_private _method follow_join_read_stream.new_target_stream()
	_loop 
		_local source << .source_stream.get()
		_if source _is _unset
		_then
			# exhausted
			_return _unset
		_else
			_local joins << _self.joined_records(source)
			_if joins.empty?.not
			_then
				_self.add_root(source)
				
				_return joins.read_stream() 
			_endif 
		_endif
	_endloop 
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_private _method follow_join_read_stream.joined_records(source)
	_local values
	_try
		_if _self.multi?(source)
		_then 
			values << source.perform(.join_field_name)
		_else
			value << source.perform(.join_field_name)
			values << {value}
		_endif 
	_when error
		values << {}
	_endtry 
	_return values
_endmethod
$


_pragma(classify_level=basic, topic={analysis, collections})
_private _method follow_join_read_stream.multi?(source)
	_if .multi? _is _unset
	_then
		.multi? << source.field(.join_field_name).simple_result?.not
	_endif
	_return .multi?
_endmethod
$
