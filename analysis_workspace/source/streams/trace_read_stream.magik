#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:trace_read_stream)
$

_pragma(classify_level=basic, topic={analysis, collections})
def_slotted_exemplar(:trace_read_stream,
	{
		{:source_geom_name,  _unset},
		{:stop_predicates,   _unset},
		{:max_length,        _unset},
		{:target_collection, _unset}
	}, :follow_base_trace_read_stream)
$

_pragma(classify_level=basic, topic={analysis, collections})
_method trace_read_stream.new(source_col, source_geom_name,
			      stop_predicates, max_length,
			      target_col)
	_return _clone.init(source_col, source_geom_name, 
			    stop_predicates, max_length,
			    target_col)
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_private _method trace_read_stream.init(source_col, source_geom_name, 
					stop_predicates, max_length, target_col)
	.source_geom_name << source_geom_name
	.stop_predicates << stop_predicates
	.max_length << max_length
	.target_collection << target_col
	
	_return _super.init(source_col)
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_private _method trace_read_stream.new_target_stream()
	_loop 
		_local source << .source_stream.get()
		_if source _is _unset
		_then
			# exhausted
			_return _unset
		_else
			_local geom << source.geometry(.source_geom_name)
			_if geom _isnt _unset
			_then
				_local traced << _self.all_traced(geom)
				_if traced.empty?.not
				_then
					_self.add_root(source)
					_return traced.read_stream()
				_endif 
			_endif 
		_endif
	_endloop 
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method trace_read_stream.all_traced(geom)
	_local start_rwo << geom.rwo
	_local rwos << rwo_set.new()
	_local nf << network_follower.new(50)
	_for i_node _over geom.all_nodes()
	_loop
		nf.trace_out(i_node, :stop_predicates, .stop_predicates, :max_cost, .max_length)
		_local travelled << nf.visited_rwos
		_for i_rec _over travelled.fast_elements()
		_loop
			_if (i_rec.rwo_type _is .target_collection.name) _andif
			    (i_rec <> start_rwo)
			_then
				rwos.add(i_rec)
			_endif
		_endloop
	_endloop 
	_return rwos
_endmethod
$
