#
#  This file is part of Analysis Workshop.
#
#  Analysis Workshop is free software: you can redistribute it and/or modify
#  it under the terms of the Lesser GNU General Public License as published by
#  the Free Software Foundation, either version 3 of the License, or
#  (at your option) any later version.
#
#  Analysis Workshop is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU Lesser General Public License
#  along with Analysis Workshop.  If not, see <http://www.gnu.org/licenses/>.
#

_package rwan
$

#remex(:trace_read_stream)
$

_pragma(classify_level=basic, topic={analysis, collections})
def_slotted_exemplar(:trace_read_stream,
	{
		{:register,          _unset},
		{:source_stream,     _unset},
		{:source_geom_name,  _unset},
		{:stop_predicates,   _unset},
		{:max_length,        _unset},
		{:target_collection, _unset},
		{:target_stream,     _unset}
	}, :input_stream_with_unget_mixin)
$

_pragma(classify_level=basic, topic={analysis, collections})
_method trace_read_stream.new(source_stream, source_geom_name,
			      stop_predicates, max_length,
			      target_col)
	_return _clone.init(source_stream, source_geom_name, 
			    stop_predicates, max_length,
			    target_col)
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_private _method trace_read_stream.init(source_stream, source_geom_name, 
					stop_predicates, max_length, target_col)
	.source_stream    << source_stream
	.source_geom_name << source_geom_name
	.stop_predicates << stop_predicates
	.max_length << max_length
	.target_collection << target_col
	.target_stream << _unset

	.register << large_record_register.new(1000)
	_return _self 
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_method trace_read_stream.get()
	_loop
		_local rec << _self.int!get()
		_if rec _is _unset _then _return _unset _endif

		_if .register.includes?(rec).not
		_then
			.register.add(rec)
			_return rec
		_endif 
	_endloop 
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_private _method trace_read_stream.int!get()
	_loop
		_if .target_stream _isnt _unset
		_then
			_local rec << .target_stream.get()
			_if rec _isnt _unset
			_then
				_return rec
			_else
				.target_stream << _unset 
			_endif
		_else
			.target_stream << _self.new_target_stream()
			_if .target_stream _is _unset
			_then
				_return _unset
			_endif 
		_endif
	_endloop 
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_private _method trace_read_stream.new_target_stream()
	# Returns unset if exhausted
	_loop 
		_local source << .source_stream.get()
		_if source _is _unset
		_then
			# exhausted
			_return _unset
		_else
			_local geom << source.geometry(.source_geom_name)
			_if geom _isnt _unset
			_then
				_local traced << _self.all_traced(geom)
				_if traced.empty?.not
				_then
					_return traced.read_stream()
				_endif 
			_endif 
		_endif
	_endloop 
_endmethod
$

_pragma(classify_level=restricted, topic={analysis})
_private _method trace_read_stream.all_traced(geom)
	_local start_rwo << geom.rwo
	_local rwos << rwo_set.new()
	_local nf << network_follower.new(50)
	_for i_node _over geom.all_nodes()
	_loop
		nf.trace_out(i_node, :stop_predicates, .stop_predicates, :max_cost, .max_length)
		_local travelled << nf.visited_rwos
		_for i_rec _over travelled.fast_elements()
		_loop
			_if (i_rec.rwo_type _is .target_collection.name) _andif
			    (i_rec <> start_rwo)
			_then
				rwos.add(i_rec)
			_endif
		_endloop
	_endloop 
	_return rwos
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_method trace_read_stream.size_is_fast?
	_return _false 
_endmethod
$

_pragma(classify_level=basic, topic={analysis, collections})
_method trace_read_stream.close()
	.register.discard()
_endmethod
$
